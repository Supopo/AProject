apply plugin: 'com.android.application'
apply plugin: 'org.jetbrains.kotlin.android'

android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion
    defaultConfig {
        applicationId rootProject.ext.android.applicationId
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode rootProject.ext.android.versionCode
        versionName rootProject.ext.android.versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        //方法超过65K，需要支持多个库
        multiDexEnabled true
        vectorDrawables {
            useSupportLibrary true
        }
    }
    //设置JDK版本
    compileOptions {
        sourceCompatibility = rootProject.ext.android.sourceCompatibility
        targetCompatibility = rootProject.ext.android.targetCompatibility
    }

    buildFeatures {
        // Enables Jetpack Compose for this module
        compose true
    }


    // 创建一个名为keystorePropertiesFile的变量，并将其初始化为rootProject文件夹中的keystore.properties文件。
    def keystorePropertiesFile = rootProject.file("keystore.properties")
    // 初始化一个名为keystoreProperties的新Properties（）对象
    def keystoreProperties = new Properties()
    // 将keystore.properties文件加载到keystoreProperties对象中
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

    signingConfigs {
        release {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
        debug {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }


    buildTypes {
        release {
            debuggable true
            minifyEnabled false //启用Proguar混淆
            shrinkResources false //是否清理无用资源,依赖于minifyEnabled
            zipAlignEnabled true //是否启用zipAlign压缩
            signingConfig signingConfigs.release

        }
        debug {
            debuggable true
            minifyEnabled false  //不启用Proguard混淆
            shrinkResources false //是否清理无用资源,依赖于minifyEnabled
            zipAlignEnabled false //是否启用zipAlign压缩
            signingConfig signingConfigs.debug
        }
    }
    dataBinding {
        enabled true
    }
    //防止代码量过大
    dexOptions {
        javaMaxHeapSize "4g"
    }
    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.6.21"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.annotation:annotation:1.0.0'
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.3.1'
    //本地化测试
    //Required -- JUnit 4 framework
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    //仪器化测试
    //Google UI测试框架
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    //内存泄漏测试 debugImplementation和releaseImplementation都要有
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.6.2'
    debugImplementation 'com.squareup.leakcanary:leakcanary-support-fragment:1.6.2'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android:1.6.2'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-support-fragment:1.6.2'

    //LibMVVM
    implementation project(path: ':LibMVVM')
    //QMUI小功能控件库
    api rootProject.ext.dependencies.qmui
    //轮播图
    api rootProject.ext.dependencies.banner
    //BaseQuickAdapter 最新版本 latest.integration 但是使用这个有些系统库可能下载不下来
    api rootProject.ext.dependencies.adapter
    //Excel解析
    api rootProject.ext.dependencies.jxl
    //文件选择器
    api rootProject.ext.dependencies.lfilepicker

    // Integration with activities
    implementation 'androidx.activity:activity-compose:1.4.0'
    // Compose Material Design
    implementation 'androidx.compose.material:material:1.1.1'
    // Animations
    implementation 'androidx.compose.animation:animation:1.1.1'
    // Tooling support (Previews, etc.)
    implementation 'androidx.compose.ui:ui-tooling:1.1.1'
    // Integration with ViewModels
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.4.1'
    // UI Tests
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4:1.1.1'

}
